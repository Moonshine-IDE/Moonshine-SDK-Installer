<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 initialize="onInitialized(event)" creationComplete="onCreationCompletes(event)">
	<fx:Metadata>
		[Event(name="DOWNLOAD_COMPONENT", type="actionScripts.events.HelperEvent")]
		[Event(name="OPEN_MOON_SETTINGS", type="actionScripts.events.HelperEvent")]
		[Event(name="COMPONENT_DOWNLOADED", type="actionScripts.events.HelperEvent")]
		[Event(name="OPEN_COMPONENT_LICENSE", type="actionScripts.events.HelperEvent")]
		[Event(name="ALL_COMPONENTS_TESTED", type="actionScripts.events.HelperEvent")]
		[Event(name="DOWNLOAD_VARIANT_CHANGED", type="actionScripts.events.HelperEvent")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			
			import actionScripts.events.HelperEvent;
			import actionScripts.interfaces.IHelperMoonshineBridge;
			import actionScripts.locator.HelperModel;
			import actionScripts.managers.InstallerItemsManager;
			import actionScripts.managers.StartupHelper;
			import actionScripts.utils.EnvironmentUtils;
			import actionScripts.valueObjects.ComponentVO;
			import actionScripts.valueObjects.HelperConstants;
			import actionScripts.valueObjects.PackageVO;
			
			import components.renderers.ComponentRenderer;
			import components.renderers.PackageRenderer;
			
			import feathers.data.ArrayCollection;
			
			//--------------------------------------------------------------------------
			//
			//  PUBLIC VARIABLES
			//
			//--------------------------------------------------------------------------
			
			public var dependencyCheckUtil:IHelperMoonshineBridge;
			public var environmentUtil:EnvironmentUtils;
			public var isRunningInsideMoonshine:Boolean;
			
			public function get isConfigurationLoaded():Boolean
			{
				return (model.components != null);
			}
			
			//--------------------------------------------------------------------------
			//
			//  PRIVATE VARIABLES
			//
			//--------------------------------------------------------------------------
			
			[Bindable] private var packageList:mx.collections.ArrayCollection;
			
			private var model:HelperModel = HelperModel.getInstance();
			private var itemsManager:InstallerItemsManager = InstallerItemsManager.getInstance();
			
			//--------------------------------------------------------------------------
			//
			//  PUBLIC API
			//
			//--------------------------------------------------------------------------
			
			public function updateComponentStatusAsDownloaded(id:String, path:String=null):void
			{
				for each (var item:ComponentVO in model.components.array)
				{
					if (item.id == id)
					{
						item.isAlreadyDownloaded = true;
						if (path) item.installToPath = path;
						this.dispatchEvent(new HelperEvent(HelperEvent.COMPONENT_DOWNLOADED, item));
						break;
					}
				}
			}
			
			public function checkForUpdate():void
			{
				if (model.components && !HelperConstants.IS_DETECTION_IN_PROCESS) 
				{
					itemsManager.environmentUtil = environmentUtil;
					itemsManager.detectOnly();
				}
			}
			
			//--------------------------------------------------------------------------
			//
			//  PROTECTED API
			//
			//--------------------------------------------------------------------------
			
			protected function onInitialized(event:FlexEvent):void
			{
				HelperConstants.IS_RUNNING_IN_MOON = isRunningInsideMoonshine;
			}
			
			protected function onCreationCompletes(event:FlexEvent):void
			{
				if (!HelperConstants.IS_RUNNING_IN_MOON) rgType.selectedIndex = 1;
				
				if (!model.components)
				{
					itemsManager.dependencyCheckUtil = dependencyCheckUtil;
					itemsManager.environmentUtil = environmentUtil;
					itemsManager.addEventListener(HelperEvent.COMPONENT_DOWNLOADED, onComponentDetected, false, 0, true);
					itemsManager.addEventListener(HelperEvent.ALL_COMPONENTS_TESTED, onAllComponentsDetected, false, 0, true);
					itemsManager.addEventListener(StartupHelper.EVENT_CONFIG_LOADED, onConfigLoaded);
					itemsManager.loadItemsAndDetect();
				}
				else
				{
					onConfigLoaded(null);
				}
			}
			
			//--------------------------------------------------------------------------
			//
			//  PRIVATE API
			//
			//--------------------------------------------------------------------------
			
			private function filterSearch(filter:Boolean=true):void
			{
				if (!filter)
				{
					//model.packages.filterFunction = null;
					model.components.filterFunction = null;
					//model.packages.refresh();
					model.components.refresh();
					return;
				}
				
				if (rgType.selectedValue == "feature")
				{
					/*model.packages.filterFunction = filterPackages;
					model.packages.refresh();*/
				}
				else
				{
					model.components.filterFunction = filterComponents;
					model.components.refresh();
				}
			}
			
			private function filterPackages(item:Object):Boolean
			{
				return !item.isIntegrated;
			}
			
			private function filterComponents(item:Object):Boolean
			{
				return !item.isAlreadyDownloaded;
			}
			
			//--------------------------------------------------------------------------
			//
			//  LISTENERS API
			//
			//--------------------------------------------------------------------------
			
			private function onConfigLoaded(event:HelperEvent):void
			{
				itemsManager.removeEventListener(StartupHelper.EVENT_CONFIG_LOADED, onConfigLoaded);
				onFilterTypeChanged(null);
				
				// pre-filter-out pre-installed items on SDK Installer view
				if (!isRunningInsideMoonshine)
				{
					model.packages.filterFunction = filterPackages;
					model.packages.refresh();
				}
			}
			
			private function onFilterTypeChanged(event:Event):void
			{
				if (rgType.selectedValue == "feature")
				{
					lstItems.itemRenderer = new ClassFactory(PackageRenderer);
					packageList = getTemporaryMXCollectionFromFeathers(model.packages);
				}
				else
				{
					lstItems.itemRenderer = new ClassFactory(ComponentRenderer);
					packageList = getTemporaryMXCollectionFromFeathers(model.components);
				}
				
				// update filter state
				onFilterChange(null);
			}
			
			private function getTemporaryMXCollectionFromFeathers(collection:feathers.data.ArrayCollection):mx.collections.ArrayCollection
			{
				var tmpCollection:mx.collections.ArrayCollection = new mx.collections.ArrayCollection();
				for each (var item:Object in collection.array)
				{
					tmpCollection.addItem(item);
				}
				return tmpCollection;
			}
			
			private function onFilterChange(event:Event):void
			{
				filterSearch(cbFilter.selected);
			}
			
			private function onComponentDetected(event:HelperEvent):void
			{
				dispatchEvent(event);
			}
			
			private function onAllComponentsDetected(event:HelperEvent):void
			{
				itemsManager.removeEventListener(HelperEvent.ALL_COMPONENTS_TESTED, onAllComponentsDetected);
				dispatchEvent(event);
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<s:RadioButtonGroup id="rgType" change="onFilterTypeChanged(event)"/>
	</fx:Declarations>
	
	<s:HGroup width="100%"
			  verticalAlign="middle">
		<s:CheckBox id="cbFilter" 
					label="Only show features that need setup"
					change="onFilterChange(event)"
					visible="{rgType.selectedValue == 'software'}"/>
		<s:Spacer width="100%"/>
		<s:RadioButton groupName="rgType" value="feature" label="By Feature"
					   selected="true"/>
		<s:RadioButton groupName="rgType" value="software" label="By Software"/>
	</s:HGroup>
	
	<s:List id="lstItems" 
			width="100%" height="100%"
			dataProvider="{packageList}"
			horizontalScrollPolicy="off" styleName="withBorderList"/>
</s:VGroup>